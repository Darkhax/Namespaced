apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'net.darkhax.namespaced'
archivesBaseName = 'namespaced'
version = '1.0.0'

repositories {
	
	mavenCentral()
}

dependencies {

    compile group: 'com.github.spotbugs', name: 'spotbugs', version: '4.0.0-beta3'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.5.1'
}

test {
	useJUnitPlatform()
	testLogging {
		events "passed", "skipped", "failed"
	}
}

task sourcesJar(type: Jar, dependsOn: classes) {

    description = 'Creates a JAR containing the source code.'
    from sourceSets.main.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {

    description = 'Creates a JAR containing the JavaDocs.'
    from javadoc.destinationDir
    classifier = 'javadoc'
}

artifacts {

    archives sourcesJar
    archives javadocJar
}

signing {

    sign configurations.archives
}

jar {
    manifest {
        attributes([
            'Timestamp': System.currentTimeMillis(),
            'Specification-Title': "Namespaced",
            'Specification-Vendor': "Darkhax",
            'Specification-Version': "${version}",
            'Implementation-Title': project.archivesBaseName,
            'Implementation-Version': project.version,
            'Implementation-Vendor' :"Darkhax",
            'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            'Built-On-Java': "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})"
        ])
    }
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
	  
      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
	  
        name 'Namespaced'
        packaging 'jar'
        description 'A library containing common components for namespaced identifiers.'
        url 'https://github.com/Darkhax/Namespaced'
		
        scm {
          connection 'scm:git:git://github.com/Darkhax/Namespaced.git'
          developerConnection 'scm:git:ssh://github.com/Darkhax/Namespaced.git'
          url 'https://github.com/Darkhax/Namespaced'
        }

        licenses {
          license {
            name 'GNU Lesser General Public License v2.1'
            url 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.en.html'
          }
        }

        developers {
          developer {
            id 'Darkhax'
            name 'Tyler Hancock'
            email 'darkhax@darkhax.net'
          }
        }
      }
    }
  }
}